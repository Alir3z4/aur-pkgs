# $Id: PKGBUILD, 2009/06/6$
# Maintainer: Alireza Savand <alireza.savand@gmail.com>
# Contributors:
#     * Maxime de Roucy <maxime.deroucy@gmail.com>
#     * Xyne <ac xunilhcra enyx, backwards>
#     * speeddefrost
#     * stefanr
#     * Bjorn Neergaard <bjorn@neersighted.com>

pkgroot=alsaequal
pkgname=alsaequal
pkgver=0.6
pkgrel=12
pkgdesc='Real-time adjustable equalizer plugin for ALSA sound server'
url='http://www.thedigitalmachine.net/alsaequal.html'
license=('GPL2')
install=alsaequal.install
arch=(i686 x86_64 armv7h)
depends=('alsa-lib' 'ladspa')
optdepends=('caps: to use the Eq CAPS equalizer (10-band)'
            'swh-plugins: to use the mbeq equalizer (15-band)')
source=("http://www.thedigitalmachine.net/tools/alsaequal-${pkgver}.tar.bz2"
        'makefile.patch'
        'false_error.patch'
        'caps_9.x.patch')
sha1sums=('aeb04e06821be82bea2e439758ca775903d62a37'
          'f21439c5c87b8c7c98035a2bbe958809e124c7ac'
          'fc780614b22261abf26f9cb93d05f10b0740dae4'
          'a3fb56bae56487cfb55492598a0fae3bf7a805ad')


prepare() {
  cd "${srcdir}/${pkgroot}"
  
  for f in ${srcdir}/*.patch; do patch < "$f"; done;
}

build() {
  cd "${srcdir}/${pkgroot}"
  make
}

package() {
  cd "${srcdir}/${pkgroot}"
  install -dm755 "${pkgdir}/usr/lib/alsa-lib"
  make DESTDIR="${pkgdir}" install
}
